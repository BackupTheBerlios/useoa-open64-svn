      SUBROUTINE IFLASH(JW)
C     *********************
C
C         ISOTHERMAL FLASH OF STREAM JW AT PRESSURE AND TEMP PRESET IN
C         PTPEN(JW,...). USES PVAP TO CALCULATE EQUILIBRIUM CONSTANTS
C         RETURNS MOL FRACTION VAPOR IN PTPEN(JW,4), COMPONENT VAPOR
C         FLOWS IN FV(I) AND COMPONENT LIQUID FLOWS IN FL(I).
C
C         WRITTEN BY R.R. HUGHES             EES IDENT  SP/IFLASH
C              LAST REVISION MAY 9, 1974
C
      REAL NUMER,PVRES
      COMMON /UNPT/ JOUT,KNTRL,KFLAG,NCPS,NPTP,NCST,NREC,NEN,WTEN(5),
     1  WEN(5,12),PTPEN(5,6),COST(5),EN(100),KTLN(15)
c     COMMON /ACT/ WTEN(5),WEN(5,12),PTPEN(5,6),COST(5),EN(100)
c     COMMON /UNACT/ JOUT,KNTRL,KFLAG,NPTP,NCST,NREC,NEN,NCPS,KTLN(15)
      COMMON /FLOS/ FV(12),FL(12),FW(12)
      COMMON /QP/ CSXX(12,28),ANAMC(12,6)
      DIMENSION  RK(12)
      DATA RKMIN/1.E-15/
C
      DO 10 I = 1,NCPS
C@+@+@+@+@+@+@+@+@+@+@+@+@+@+@+@+@+@+@+@+@+@+@+@+@+@+@+@+@+@+
       CALL PVAP(I,PTPEN(JW,2),PVRES)
       RK(I) = PVRES / PTPEN(JW,1)
      IF(RK(I).GE.RKMIN) GO TO 10
      WRITE(JOUT,8010) I
        RK(I)=RKMIN
  10  CONTINUE
C
C              FIRST TRY METHOD FOR LOW VAPORIZATION
        MCAL = 1
        PSI = 1.
C
C         START BASIC ALGORITHMS
   20 ASSIGN 35 TO MDO
C
      DO 50 K = 1,20
        TEST = -WTEN (JW)
        DERIV = 0.
C
      DO 30 I = 1,NCPS
C
      GO TO (24,26), MCAL
   24   NUMER = (1.-1./RK(I))
        DENOM = 1. - PSI * NUMER
      GO TO 28
   26   NUMER = RK(I) - 1.
        DENOM = RK(I) - PSI * NUMER
C
   28   TEST = TEST + WEN(JW,I)/DENOM
   30   DERIV = DERIV + WEN(JW,I)*NUMER/DENOM**2
C
      GO TO MDO , (35,40)
   35 IF (TEST.LE.0.) GO TO (200,300), MCAL
C              (SINGLE PHASE, - OUTSIDE BUBBLE PRESSURE/DEW PRES. RANGE)
      ASSIGN 40 TO MDO
   40   DEL = TEST/DERIV
        PSI = PSI - DEL
      IF (ABS(DEL).LE.1.E-6) GO TO 100
C
C         FOR LARGE VAPORIZATION, SWITCH TO ALTERNATIVE CALCULATION
      IF (MCAL.EQ.1.AND.PSI.LT.0.4) GO TO 60
   50 CONTINUE
C
      WRITE(JOUT,8050) MCAL,DEL,PSI
      GO TO 100
C
C         SECOND METHOD USES DIFFERENT TEST FUNCTION
   60   MCAL = 2
        PSI = 0.
      GO TO 20
C
C         PERCENT VAPORIZED ESTABLISHED - CALCULATION DEPENDS ON DEGREE
C         OF VAPORIZATION
  100 GO TO (110,130), MCAL
  110 IF (PSI.GT.0.999999) GO TO 200
        R = PSI/(1.-PSI)
      DO 120 I = 1,NCPS
        FV(I) = WEN(JW,I)/(1.+R/RK(I))
  120   FL(I) = WEN(JW,I) - FV(I)
      GO TO 900
C
  130 IF (PSI.LT.1.E-6)GO TO 300
        R =(1.-PSI)/PSI
      DO 140 I = 1,NCPS
        FL(I) = WEN(JW,I)/(1.+R*RK(I))
  140   FV(I) = WEN(JW,I) - FL(I)
      GO TO 900
C
C         LIQUID ONLY, - AT OR ABOVE THE BUBBLE PRESSURE
  200 DO 210 I = 1,NCPS
        FL(I) = WEN(JW,I)
  210   FV(I) = 0.
      GO TO 900
C
C         VAPOR ONLY, - AT OR BELOW DEW PRESSURE
  300 DO 310 I = 1,NCPS
        FV(I) = WEN(JW,I)
  310   FL(I) = 0.
C
  900   PTPEN(JW,4) = 1.-PSI
      IF (KFLAG.LE.1) GO TO 910
      WRITE(JOUT,8910) (RK(I),FV(I),FL(I),
     1 (ANAMC(I,JJ),JJ=1,6),I=1,NCPS)
  910 RETURN
C
 8010 FORMAT(13H0** COMPONENT ,I4,23H SHOWS NEGL. VOLATILITY )
 8050 FORMAT (22H0*** FLASH CALCN, TYPE, I3,31H, FAILED TO CNVRGE IN 20
     1ITER -/ 10X,5HDEL =, G13.3,11H, FOR PSI =,G13.6 )
 8910 FORMAT (10X,4HK(I),8X,11HVAPOR FLOWS,5X,9HLIQ FLOWS,
     1 3X,'COMPONENT',/,(5X,3G15.5,3X,6A3 ))
C
      END
