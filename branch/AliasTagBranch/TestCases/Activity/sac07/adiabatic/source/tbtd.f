      SUBROUTINE TBTD (KS,JW,P,TB,TD,HB,HD)
C     *******************************
C
C         CALCULATES BUBBLE AND DEW-POINT TEMPERATURES (TB AND TD) FOR
C         STREAM ASSOCIATED WITH POINT JW, AT PRESSURE P, USING VALUES
C         PRESET IN TB AND TD AS INITIAL ESTIMATES.  KS INDICATES OPTION
C              KS = 1  -  BUBBLE-POINT ONLY
C              KS = 2  -  DEW-POINT ONLY
C              KS = 3  -  BOTH BUBBLE- AND DEW-POINT
C         IF NON-CONDENSABLES OR NON-VOLATILES ARE FOUND
C         TB OR TD CALCULATED ON THE BASIS OF EQLM. VAP. FR.
C         OR EQLM. LIQ. FR. DEFINED AS VFAC*(TOTAL MOLES OF
C         NON-CONDENSABLES OR NON-VOLATILES AS THE CASE MAY BE)
C
C         WRITTEN BY R.R. HUGHES & R.K. MALIK     EES IDENT SP/TBTD
C              LAST REVISION SEPTEMBER 9,1974
C
C+@+@+@+@+@+@+@@+@+@+@+@+@+@+@+@+@+@+@+@+@+@+@+@+@+@@+
      REAL PVRES
      COMMON/QP/ CSXX(12,28),ANAMC(12,6)
      COMMON /UNPT/ JOUT,KNTRL,KFLAG,NCPS,NPTP,NCST,NREC,NEN,WTEN(5),
     1  WEN(5,12),PTPEN(5,6),COST(5),EN(100),KTLN(15)
c     COMMON /ACT/ WTEN(5),WEN(5,12),PTPEN(5,6),COST(5),EN(100)
c     COMMON /UNACT/ JOUT,KNTRL,KFLAG,NPTP,NCST,NREC,NEN,NCPS,KTLN(15)
      DIMENSION NMOPT(2),KJ(12),ZF(12),AKE(12)
      DATA VFAC/0.1/, LIM2/50/
      DATA NMOPT/3HBBL,3HDEW/
      DATA  TZ/-459.67/,PTOL/1.E-10/,WTOL/1.E-3/,TLIM/5000./
      DATA EPS/1.E-3/, LIM/50/, FR/0.02/, DL/-30./, DH/30./
C
C         CHECK DATA, INITALIZE, AND SET GATES
2     IF (P.LE.PTOL.OR.WTEN(JW).LE.WTOL) GO TO 500
      IF(TB.LT.TZ.OR.TB.GT.TLIM)TB=0.
      IF(TD.LT.TZ.OR.TD.GT.TLIM)TD=0.
C
C         CHECK IF NON CONDENSABLES ARE PRESENT
C
        AKBARL = 0.
      DO 5 I = 1,NCPS
C+@+@+@+@+@+@+@+@+@+@+@+@+@+@+@+@+@+@+@+@+
      CALL PVAP(I,TB,PVRES)
      AKE(I) = PVRES/P
    5 AKBARL = AKBARL + WEN(JW,I)/WTEN(JW)*LOG10(AKE(I)+1.E-10)
        AKBAR = 10.**AKBARL
      IF (KS.EQ.2) GO TO 7
      DO 6 I= 1,NCPS
        RATIOK = AKE(I)/AKBAR
      IF (RATIOK.GE.100..AND.WEN(JW,I).GT.0.) GO TO 351
    6 CONTINUE
C
    9   T = TB
        D = P * WTEN(JW)
        MS = 1
      GO TO 20
C
C         CHECK IF NON-VOLATILES ARE PRESENT
C
    7 DO 8 I= 1,NCPS
        RATIOK = AKE(I)/AKBAR
      IF (RATIOK.LE.0.01.AND.WEN(JW,I).GT.0.) GO TO 400
    8 CONTINUE
C
C
   10   T = TD
        D = WTEN(JW)/P
        MS = 2
C
   20 IF (T.LT.TZ.OR.T.GT.TLIM) T=0.
      ASSIGN 110 TO MB
C
C         BEGIN MAJOR ITERATION LOOP FOR TEMPERATURE ADJUSTMENT
      DO 200 J = 1,LIM
        DEL = D
C
C         BEGIN MINOR ITERATION LOOP FOR COMPONENT SUMMATION
      DO 100 I = 1,NCPS
        W = WEN(JW,I)
      IF (W.LE.WTOL) GO TO 100
C+@+@+@+@+@+@+@+@+@+@+@+@+@+@+@+@+@+@@+@+@+@+@+@+@+@+@+@+@+@+
       CALL PVAP(I,T,PVRES)
       PV = PVRES      
      GO TO (70,80), MS
C
   70   DEL = DEL - W*PV
      GO TO 100
C
   80 IF (PV.LE.PTOL) GO TO 150
        DEL = DEL - W/PV
  100 CONTINUE
C
C         CHECK FOR COMPLETION
        DEL = DEL/D
      IF (ABS(DEL).LE.EPS) GO TO 300
C
C         SET UP NEXT ITERATION
      GO TO MB, (110,120)
C
C              FIRST TIME - ARBITRARY DT
  110 ASSIGN 120 TO MB
        DT = SIGN((FR*(T-TZ)),DEL)
      IF (MS.EQ.2) DT = -DT
      GO TO 130
C
C              LATER TRIES - USE NEWTONS METHOD
 120    DELP=(DELP-DEL)/DEL
      IF(ABS(DELP).GT.EPS) GO TO 128
      GO TO 110
 128  DT=DT/DELP
C
  130   DT = AMAX1(DL,MIN(DH,DT))
        DELP = DEL
      GO TO 200
C
C         FOR LOW VOLATILITY IN DEW POINT CALCULATION, RAISE TEMPERATURE
C         ARBITRARILY
  150   DT = DH
      ASSIGN 110 TO MB
C
  200   T = T + DT
C
C
C         PRINT ERROR FLAG IF DO-LOOP HAS NORMAL EXIT
210     NMP = NMOPT(MS)
       WRITE(JOUT,9200) NMP,LIM,P,TB,TD,DT,DEL,DELP,EPS,WTEN(JW),
     1  (WEN(JW,K),K = 1,NCPS)
C
C         ONE CALCULATION COMPLETE, - STORE RESULT AND CHECK SPECS FOR 2ND
  300 GO TO (310,320), MS
C
  310   TB = T
      IF (KS.EQ.3) GO TO 7
      GO TO 330
  320   TD = T
330   IF(TB.LT.TZ.OR.TB.GT.TLIM)GO TO 335
      IF(TD.GT.TZ.AND.TD.LT.TLIM)GO TO 340
335   WRITE(JOUT,9355) TB,TD
      STOP
  340 IF (KFLAG.LE.1) GO TO 350
      WRITE(JOUT,9330) P
      IF (KS.NE.2) WRITE(JOUT,9331) TB
      IF (KS.GT.1) WRITE(JOUT,9332) TD
  350 RETURN
C
C         CALCULATION OF BUBBLEPOINT - NONCONDENSABLES CASE
351    CONTINUE
        J = 0
        SUMZ = 0.
      DO 360 I = 1,NCPS
        RATIOK = AKE(I)/AKBAR
      IF (RATIOK.LT.100.) GO TO 360
        J = J+1
        ZF(J) = WEN(JW,I)/WTEN(JW)
        KJ(J) = I
        SUMZ = SUMZ + ZF(J)
  360 CONTINUE
        VF = (1.+VFAC)*SUMZ
      VF=AMAX1(0.001,VF)
      WRITE(JOUT,9350) VF
C         USE NEWTON'S METHOD
        T = TB
        I1 = 1
      DO 370 I = 1,LIM2
        SUMD = 0.
      DO 365 K = 1,NCPS
      DO 366 KK = 1,J
  366 IF (K.EQ.KJ(KK)) GO TO 365
C+@+@+@+@+@+@+@+@+@+@+@+@+@+@+@+@+@+@@+@+@+@+@+@++@+@+@+@+@
       CALL PVAP(K,T,PVRES)
       PVK=PVRES/P
      SUMD=SUMD+(WEN(JW,K)/WTEN(JW))*(PVK-1.)/(VF*PVK+1.-VF)
  365 CONTINUE
        FUNXON = SUMD + 1./(1.+VFAC)
      IF(ABS(FUNXON).LT.EPS)GO TO 310
      IF(I.GT.1)GO TO 368
      DFUNC=0.
      GO TO 369
368   DFUNC=DT/(FUNXON-FUNCS)
369   DT=-FUNXON*SIGN(AMAX1(1.E-2,ABS(DFUNC)),DFUNC)
      DT=AMAX1(DL,MIN(DH,DT))
      IF(ABS(DT).LE.EPS)GO TO 310
      FUNCS=FUNXON
371   IF((T+DT).GT.TZ.AND.(T+DT).LT.TLIM)GO TO 370
      DT=DT/2.
      GO TO 371
370   T=T+DT
      MS=1
      GO TO 210
C
C         CALCULATION OF DEW POINT NON-VOLATILES CASE
C
  400 IF (KS.EQ.1) GO TO 9
        J = 0
        SUMZ = 0.
      DO 410 I = 1,NCPS
      RATIOK = AKE(I)/AKBAR
      IF (RATIOK.GT..01) GO TO 410
        J = J + 1
        ZF(J) = WEN(JW,I)/WTEN(JW)
        KJ(J) = I
        SUMZ = SUMZ + ZF(J)
  410 CONTINUE
        ALF = (1.+VFAC)*SUMZ
      ALF=AMAX1(0.001,ALF)
        VF = 1. - ALF
      WRITE(JOUT,9400) ALF
C         USE NEWTON'S METHOD
        T = TD
        I1 = 1
      DO 420 I = 1,LIM2
        SUMD = 0.
      DO 418 K = 1,NCPS
      DO 419 KK = 1,J
  419 IF (K.EQ.KJ(KK)) GO TO 418
C+@+@+@+@+@++@+@+@+@@@@@@@@@++++++++++++++++++++++++
	CALL PVAP(K,T,PVRES)
      PVK=PVRES/P
      SUMD=SUMD+WEN(JW,K)/WTEN(JW)*(PVK-1.)/(VF*PVK+1.-VF)
  418 CONTINUE
        FUNXON = SUMD + 1./(1.+VFAC)
      IF(ABS(FUNXON).LT.EPS)GO TO 320
      IF(I.GT.1)GO TO 412
      DFUNC=0.
      GO TO 413
412   DFUNC=DT/(FUNXON-FUNCS)
413   DT=-FUNXON*SIGN(AMAX1(1.E-2,ABS(DFUNC)),DFUNC)
      DT=AMAX1(DL,MIN(DH,DT))
      IF(ABS(DT).LT.EPS)GO TO 320
      FUNCS=FUNXON
414   IF((T+DT).GT.TZ.AND.(T+DT).LT.TLIM)GO TO 420
      DT=DT/2.
      GO TO 414
420   T=T+DT
      MS=2
      GO TO 210
C
  500 WRITE(JOUT,9500) KS,JW, P,TB,TD,WTEN(JW)
      RETURN
C
C
 9200 FORMAT ( 6H0**** ,A3,34H-POINT CALCN HAS NOT CONVERGED IN ,I3,
     1 13H ITERATIONS -/6X,22HDATA AND RESULTS ARE - / 6X,
     2  3HP = , F10.4, 6H, TB =, F10.4, 6H, TD =, F10.4, 6H, DT =,F10.4/
     3  6X,5HDEL =,F12.8,14H, DEL(PREV.) =,F12.8, 7H, EPS =, F12.8 /
     4  6X,29HFLOWS (LB MOLS/HR) - TOTAL = ,F12.3/ (3X,6F11.3) )
C
 9330 FORMAT (3H AT, F10.5,6H PSIA, )
 9331 FORMAT (1H+,20X,9HBBLE PT = ,F10.5,4H DF, )
 9332 FORMAT (1H+,44X,8HDEW PT = ,F10.5)
 9350 FORMAT (57H0****NON-CONDENSABLES PRESENT,BUBBLE POINT CAL. WITH VF
     1= ,F6.3)
 9355 FORMAT(/,10X,'BUBBLE AND/OR DEW POINT TEMPS OUT OF BOUNDS',2E20.4)
 9400 FORMAT (51H0****NON-VOLATILES PRESENT,DEW POINT CAL. WITH LF= ,
     1F6.3)
C
 9500 FORMAT (32H0**** TBTD HAS BAD DATA --- KS =,I3, 6H, JW =, I3 / 6X,
     1    3HP =,F10.4, 6H, TB =,F10.4,6H, TD =, F10.4,6H, WT =, F12.3 )
C
      END
