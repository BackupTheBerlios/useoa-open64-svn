	parameter(n=100)
	parameter(step=0.1)

	do L=0,10
		print *, 'L=',L
		z = 0.1
		do i=1,n
			call rbes(L,z,zjn,zjnp,zyn,zynp)
			write(6,100) z,zjn,zjnp,zyn,zynp
			z = z + step
		enddo
	enddo

  100	format(f10.3,4e15.5)
	stop
	end

      SUBROUTINE RBES(N,Z,ZJN,ZJNP,ZYN,ZYNP)
c      IMPLICIT DOUBLE PRECISION (A-H,O-Z) 
c      SAVE                                                              MJU3-12
C***********************************************************************
C***  THIS SUBROUTINE CALCULATES THE REGULAR RICATTI-BESSEL FUNCTION ZJN
C***AND ITS DERIVATIVE ZJNP AND THE IRREGULAR RICATTI-BESSEL FUNCTION 
C***ZYN AND ITS DERIVATIVE ZYNP OF ORDER N FOR ARGUMENT Z.  THIS IS 
C***A COPY OF LIGHTS ROUTINE ATTRIBUTED TO R.GORDON.
C***********************************************************************

      DATA ZERO,HALF,ONE,TWO,THREE/0.0,0.5,1.0,2.0,3.0/ 

C***EVALUATE TRIG FUNCTIONS 

      SZ=SIN(Z) 
      CZ=COS(Z) 
      ZINV=ONE/Z
      AJ=SZ 
      BJ=SZ*ZINV-CZ 
      AY=-CZ
      BY=-SZ-CZ*ZINV

      IF(N-1)1,2,3

C***FUNCTIONS OF ZERO ORDER 

    1 ZJN=AJ
      ZJNP=CZ 
      ZYN=AY
      ZYNP=SZ 
      RETURN

C***FUNCTIONS OF ORDER ONE

    2 ZJN=BJ
      ZJNP=AJ-ZINV*BJ 
      ZYN=BY
      ZYNP=AY-ZINV*BY 
      RETURN

C***FUNCTIONS OF ORDER GREATER THAN ONE 

    3 XN=N  
      DELF=ZINV+ZINV
      FACTOR=DELF+ZINV

C***TEST TO SEE IF RECURRENCE CAN BE USED 

      IF(Z.GT.XN)GO TO 100

C***IN NON CLASSICAL REGION USE RECURSION RELATION FOR IRREGULAR
C***SOLUTION AND ASCENDING POWER SERIES FOR REGULAR SOLUTION. 
C***THE COEFFICIENT,C, IN FRONT OF POWER SERIES IS CALCULATED DURING  
C***FORWARD RECURRENCE. 

      ZSQ=Z*Z 
      TOP=XN+XN 
      XJ=THREE
      C=ZSQ/THREE 

C***IRREGULAR SOLUTION

   25 ZYN=BY*FACTOR-AY
      AY=BY 
      BY=ZYN
      FACTOR=FACTOR+DELF
      XJ=XJ+TWO 
      C=C*(Z/XJ)
      IF(XJ.LT.TOP)GO TO 25 
      ZYNP=AY-ZINV*BY*XN

C***REGULAR SOLUTION

      FACTOR=-HALF*ZSQ
      U=ONE 
      TERM=ONE
      DU=ONE
      DTERM=ONE 
      D2NP3=TOP+THREE 
      DEN=D2NP3 
      DFACT=ZERO

   35 DFACT=DFACT+ONE 

      TERM=TERM*(FACTOR/(DFACT*DEN))
      U=U+TERM
      DEN=DEN+TWO 
      DTERM=DTERM*(FACTOR/(DFACT*DEN))
      DU=DU+DTERM 

C***TEST CONVERGENCE TO SINGLE PRECISION ACCURACY 

      IF(ABS(TERM).GT.1.0D-9)GO TO 35 
      ZJN=U*C 
      ZJNP=(XN+ONE)*ZJN*ZINV-(Z*DU/D2NP3)*C 
      RETURN

C***FOR CLASSICAL CASE USE FORWARD RECURSION FOR REGULAR
C***AND IRREGULAR SOLUTIONS 

  100 CONST=ZINV*XN 
      TOP=CONST+CONST 

  200 AJ=FACTOR*BJ-AJ 
      AY=FACTOR*BY-AY 
      FACTOR=FACTOR+DELF
      IF(FACTOR.GT.TOP)GO TO 250
      BJ=FACTOR*AJ-BJ 
      BY=FACTOR*AY-BY 
      FACTOR=FACTOR+DELF
      IF(FACTOR.LT.TOP)GO TO 200
      ZJN=BJ
      ZJNP=AJ-CONST*BJ
      ZYN=BY
      ZYNP=AY-CONST*BY
      RETURN

  250 ZJN=AJ

      ZJNP=BJ-CONST*AJ
      ZYN=AY
      ZYNP=BY-CONST*AY

      END 
