From bischof Tue Oct 29 11:41:25 1991
Return-Path: <bischof>
Received: from canopus.mcs.anl.gov by antares.mcs.anl.gov (4.1/SMI-GAR)
	id AA01697; Tue, 29 Oct 91 11:41:19 CST
Received: from localhost by canopus.mcs.anl.gov (4.1/GCF-5.8)
	id AA27202; Tue, 29 Oct 91 11:41:15 CST
Message-Id: <9110291741.AA27202@canopus.mcs.anl.gov>
To: hovland
Subject: 
Date: Tue, 29 Oct 91 11:41:11 -0600
From: bischof
Status: OR


------- Forwarded Message

Return-Path: shubin@espresso.boeing.com
Return-Path: <shubin@espresso.boeing.com>
Received: from atc.boeing.com by antares.mcs.anl.gov (4.1/SMI-GAR)
	id AA00939; Tue, 29 Oct 91 09:44:38 CST
Received: by atc.boeing.com on Tue, 29 Oct 91 07:41:40 PST
Received: from sisyphus.bcs.eca by espresso.boeing.com (4.1/SMI-4.1)
	id AA00875; Tue, 29 Oct 91 07:46:44 PST
Date: Tue, 29 Oct 91 07:46:44 PST
From: shubin@espresso.boeing.com (Greg Shubin 5-3516)
Message-Id: <9110291546.AA00875@espresso.boeing.com>
To: bischof@antares.mcs.anl.gov

CHRIS: here is an annotated version of NUMJAC that I hope clarifies things. 
If not, pls call.  Thanks...Greg Shubin

      subroutine numjac
      implicit double precision(a-h,o-z)
      common //
     1 u(190),eq(190),eqold(190),a(190,95),eqplus(190),
     2 b(10),bth(10),ff(3,6,10),gg(3,6,10),hh(3,6,10),
     3 mc,nc,dx,dy,ddx,ddy,ddx2,ddy2,mc1,nc1,mc2,mc3,
     4 theta1,gamma,gm1,xmach,ncf,mcncf,
     5 qinc,dinc,pinc,einc,ainc,
     6 htot,swall,dstag,emaxs(200),eqmaxs(200),delta,
     7 kdiag,kblk,ksize,kblk2,kblk3
     8 ,itmax,sstol,alpha,nprint,emax,eqmax
     9 ,kprint,dtheta,icont,xend,xincr
      dimension ffplus(3),ggplus(3),hhplus(3),kbegin(5),eqp(3)
      real timvec(2),etime,dummy
      external etime
c
c         compute rowwise numerical jacobian.
c         successively perturb each variable (column) which appears
c         in an equation (row).
c
c         note:  in the banded solver (ICBAND) storage scheme, the
c         matrix entry that would be a(i,j) in dense storage mode
c         is stored in a(i,j-i+kdiag), where kdiag is suitably chosen
c         to represent the diagonal entry (kdiag is in common)
c
c         THE JACOBIAN IS COMPUTED ONE ROW OF MATRIX "A" AT A TIME.
c
c         THE COMPUTATIONAL GRID IS NUMBERED (M,N), M=1 TO MC,
c         N=1 TO NC.  
c
c         AT GRID POINTS WITH N=1 (THE WALL) THERE ARE
c         3 COMPUTATIONAL UNKNOWNS AND THREE ASSOCIATED
c         "WALL" EQUATIONS
c
c         AT GRID POINTS WITH N=2 TO NC-1 THERE ARE 3 COMPUTATIONAL
c         UNKNOWNS AND THREE ASSOCIATED "INSIDE" EQUATIONS 
c
c         AT GRID POINTS WITH N=NC THERE ARE 4 COMPUTATIONAL 
c         UNKNOWNS AND FOUR ASSOCIATED "SHOCK" EQUATIONS
c
c         THE UNKNOWNS (COLUMNS OF JACOBIAN) ARE ORDERED AS FOLLOWS:
c
c             ((  3 wall unknowns (N=1), 
c                (3 interior unknowns, N=2, NC-1),
c                 4 shock unknowns (N=NC)                 ), M=1,MC)
c
c         THE EQUATIONS (ROWS OF JACOBIAN) ARE ORDERED AS FOLLOWS:
c
c             ((  3 wall equations (N=1), 
c                (3 interior equations, N=2, NC-1),
c                 4 shock equations (N=NC)                 ), M=1,MC)
c
c         THAT IS, WE LOOP OVER COLUMNS (M) IN THE COMPUTATIONAL
c         GRID, AND FOR EACH M, WE LOOP OVER N FROM THE WALL N=1, 
c         THROUGH THE "INSIDE" (N=2 TO NC-1), TO THE SHOCK
c         N=NC.
c
c         THUS, IN THE NC=6 BY MC=10 GRID USED HERE, 
c         THE WALL EQUATIONS (ROWS IN JACOBIAN) ARE (1,2,3), (20,21,22), ... 
c         (172,173,174).
c
c         THE INSIDE EQUATIONS ARE (4,5,6,...,13,15,15),
c                                  (23,24,25,...,32,33,34),
c                                  .........
c                                  (175,176,177,...184,185,186)
c
c         THE SHOCK EQUATIONS ARE   (16,17,18,19),
c                                   (35,36,37,38),
c                                   .........
c                                   (187,188,189,190)
c                                   
c             
c
c         
c
      dummy=etime(timvec)
      t1=timvec(1)
c         zero out jacobian
      do 10 k=1,ksize
      do 10 m=1,mcncf
10    a(m,k)=0.
c
c         loop on columns OF THE GRID m=1,mc
c
      do 1000 m=1,mc
c
c         loop on n=1 to nc
c           n=1 wall 3 eqns
c           n=2 to nc1 3 eqns
c           n=nc 4 eqns
c
      kdiagb=(m-1)*ncf+1
c
c         wall eqns
c
      if(m .ne. 1)go to 50
c         special case m=1 , stag pt, gives diagonal block
      do 40 i=1,3
40    a(i,kdiag)=1.
      go to 65
50    continue
c         derivs wrt flow variables
      kbegin(1)=kdiagb-kblk
      kbegin(2)=kdiagb
      kbegin(3)=kdiagb+3
      kbegin(4)=kdiagb+6
      kbegin(5)=kdiagb+kblk
      if(m .eq. mc)kbegin(5)=kdiagb-kblk2
      do 60 i=1,5
60    call jacob(1,m,kbegin(i),kbegin(i)+2)
65    continue
c         derivs wrt shock locations
      call mshk(m,mf,mb)
      ilast=mf-mb+1
      do 70 i=1,ilast
      kbegin(i)=(mb+i-1)*ncf
      call jacob(1,m,kbegin(i),kbegin(i))
70    continue
c
c
c         interior EQUATIONS
c
c
      do 900 n=2,nc1
      kdiagb=(m-1)*ncf+3*(n-1)+1
c
c         variables at n-1,m affect ff(ii,n-1,m)
c
      kbeg=kdiagb-3
      kend=kbeg+2
      do 100 k=kbeg,kend
      usave=u(k)
      u(k)=u(k)+delta
      call eval(n-1,m,1,0,ffplus(1),dum1,dum2)
      do 110 ii=1,3
      idiag=(m-1)*ncf+(n-1)*3+ii
      index=k-idiag+kblk3+1
110   a(idiag,index)=-(ffplus(ii)-ff(ii,n-1,m))*ddx2/delta
      u(k)=usave
100   continue
c
c         variables at n,m-1 affect gg(ii,n,m-1)
c         special case m=mc
c
      if(m .eq. 1) go to 201
      factor=-1.
      if(m .eq. mc) factor=-4.
      kbeg=kdiagb-kblk
      kend=kbeg+2
      do 200 k=kbeg,kend
      usave=u(k)
      u(k)=u(k)+delta
      call eval(n,m-1,2,0,dum1,ggplus(1),dum2)
      do 210 ii=1,3
      idiag=(m-1)*ncf+(n-1)*3+ii
      index=k-idiag+kblk3+1
210   a(idiag,index)=factor*(ggplus(ii)-gg(ii,n,m-1))*ddy2/delta
      u(k)=usave
200   continue
201   continue
c
c         variables at n,m affect hh(ii,n,m)
c         special case m=mc gg(ii,n,m) also affected
      kbeg=kdiagb
      kend=kbeg+2
      iflag=0
      if(m .eq. 1)iflag=1
      do 300 k=kbeg,kend
      usave=u(k)
      u(k)=u(k)+delta
      call eval(n,m,3,iflag,dum1,dum2,hhplus(1))
      if(m .eq. mc)call eval(n,m,2,0,dum1,ggplus(1),dum2)
      do 310 ii=1,3
      idiag=(m-1)*ncf+(n-1)*3+ii
      index=k-idiag+kblk3+1
      a(idiag,index)=(hhplus(ii)-hh(ii,n,m))/delta
      if(m .eq. mc)a(idiag,index)=a(idiag,index)
     1 +3.*(ggplus(ii)-gg(ii,n,m))*ddy2/delta
310   continue
      u(k)=usave
300   continue
c
c         variables at n,m+1 affect gg(ii,n,m+1)
c         special case m=1--h affected but not g
c
      if(m .eq. mc)go to 401
      kbeg=kdiagb+kblk
      kend=kbeg+2
      do 400 k=kbeg,kend
      usave=u(k)
      u(k)=u(k)+delta
      if(m .ne. 1)call eval(n,m+1,2,0,dum1,ggplus(1),dum2)
      if(m .eq. 1)call eval(n,m,3,1,dum1,dum2,hhplus(1))
      do 410 ii=1,3
      idiag=(m-1)*ncf+(n-1)*3+ii
      index=k-idiag+kblk3+1
      if(m .ne. 1)a(idiag,index)=(ggplus(ii)-gg(ii,n,m+1))*ddy2/delta
      if(m .eq. 1)a(idiag,index)=(hhplus(ii)-hh(ii,n,1))/delta
410   continue
      u(k)=usave
400   continue
401   continue
c
c         variables at n+1,m affect ff(ii,n+1,m)
c
      kbeg=kdiagb+3
      kend=kbeg+2
      do 500 k=kbeg,kend
      usave=u(k)
      u(k)=u(k)+delta
      call eval(n+1,m,1,0,ffplus(1),dum1,dum2)
      do 510 ii=1,3
      idiag=(m-1)*ncf+(n-1)*3+ii
      index=k-idiag+kblk3+1
      a(idiag,index)=(ffplus(ii)-ff(ii,n+1,m))*ddx2/delta
510   continue
      u(k)=usave
500   continue
c
c         variables at n,m-2 affect gg(ii,n,mc-2) at m=mc only
c
      if(m .ne. mc)go to 601
      kbeg=kdiagb-kblk2
      kend=kbeg+2
      do 600 k=kbeg,kend
      usave=u(k)
      u(k)=u(k)+delta
      call eval(n,mc2,2,0,dum1,ggplus(1),dum2)
      do 610 ii=1,3
      idiag=(m-1)*ncf+(n-1)*3+ii
      index=k-idiag+kblk3+1
      a(idiag,index)=(ggplus(ii)-gg(ii,n,mc2))*ddy2/delta
610   continue
      u(k)=usave
600    continue
601   continue
c
c         derivs wrt shock locations
c
      call mshk(m,mf,mb)
      do 700 k=mb,mf
      kb=k*ncf
      usave=u(kb)
      u(kb)=u(kb)+delta
      call single(n,m,eqp)
      do 710 ii=1,3
      idiag=(m-1)*ncf+(n-1)*3+ii
      index=kb-idiag+kblk3+1
      a(idiag,index)=(eqp(ii)-eq(idiag))/delta
710   continue
      u(kb)=usave
700   continue
701   continue
c
c        end loop on n
c
900   continue
c
c
c         equations at the shock
c
c
      kdiagb=(m-1)*ncf+3*(nc-1)+1
c
c         derivs wrt flow variables
c
      kbegin(1)=kdiagb-kblk
      kbegin(2)=kdiagb-6
      kbegin(3)=kdiagb-3
      kbegin(4)=kdiagb
      kbegin(5)=kdiagb+kblk
      if(m .eq. mc)kbegin(5)=kdiagb-kblk2
      istart=1
      if(m .eq. 1)istart=2
      do 750 i=istart,5
750   call jacob(nc,m,kbegin(i),kbegin(i)+2)
c
c         derivs wrt shock locations
c
      call mshk(m,mf,mb)
      ilast=mf-mb+1
      do 770 i=1,ilast
      kbegin(i)=(mb+i-1)*ncf
      call jacob(nc,m,kbegin(i),kbegin(i))
770   continue
c
c         end loop on m
c
1000  continue
      dummy=etime(timvec)
      t2=timvec(1)
      write(6,5000)t2-t1
5000  format(/," numjac time in seconds=",f10.4)
      return
      end
c**********************************************************************

------- End of Forwarded Message


